#####################
Esta gramática teve permanência em LL(1) verificada pelo script que entregamos como exercício em aula.
#####################

PROGRAM -> STATEMENT 
PROGRAM -> FUNCLIST 
PROGRAM -> ''

FUNCLIST -> FUNCDEF FUNCLIST'

FUNCLIST' -> FUNCLIST 
FUNCLIST' -> ''

FUNCDEF -> def ident ( PARAMLIST ) { STATELIST }

PARAMLIST -> ''
PARAMLIST -> int ident PARAMLIST'
PARAMLIST -> float ident PARAMLIST'
PARAMLIST -> string ident PARAMLIST'

PARAMLIST' -> , PARAMLIST
PARAMLIST' -> ''

STATEMENT -> VARDECL ;
STATEMENT -> ATRIBSTAT ;
STATEMENT -> PRINTSTAT ;
STATEMENT -> READSTAT ;
STATEMENT -> RETURNSTAT ;
STATEMENT -> IFSTAT
STATEMENT -> FORSTAT 
STATEMENT -> { STATELIST } 
STATEMENT -> break ;
STATEMENT -> ;

VARDECL -> int ident OPVARDECL
VARDECL -> float ident OPVARDECL
VARDECL -> string ident OPVARDECL

OPVARDECL -> [ int_constant ] OPVARDECL
OPVARDECL -> ''

ATRIBSTAT -> LVALUE = ATRIBSTAT' 

ATRIBSTAT' -> EXPRESSION 
ATRIBSTAT' -> ALLOCEXPRESSION 
ATRIBSTAT' -> FUNCCALL

FUNCCALL -> call ident ( PARAMLISTCALL )

PARAMLISTCALL -> ident PARAMLISTCALL' 
PARAMLISTCALL -> ''

PARAMLISTCALL' -> , PARAMLISTCALL 
PARAMLISTCALL' -> ''

PRINTSTAT -> print EXPRESSION

READSTAT -> read LVALUE

RETURNSTAT -> return

IFSTAT -> if ( EXPRESSION ) STATEMENT IFSTAT'

IFSTAT' -> else STATEMENT
IFSTAT' -> ''

FORSTAT -> for ( ATRIBSTAT ; EXPRESSION ; ATRIBSTAT ) STATEMENT

STATELIST -> STATEMENT STATELIST' 
STATELIST' -> STATELIST 
STATELIST' -> ''

ALLOCEXPRESSION -> new ALLOCEXPRESSION'

ALLOCEXPRESSION' -> int NUMEXPR1
ALLOCEXPRESSION' -> float NUMEXPR1
ALLOCEXPRESSION' -> string NUMEXPR1

NUMEXPR1 -> [ NUMEXPRESSION ] NUMEXPRLIST

NUMEXPRLIST -> [ NUMEXPRESSION ] NUMEXPRLIST
NUMEXPRLIST -> ''

EXPRESSION -> NUMEXPRESSION EXPRESSION'

EXPRESSION' -> < EXPRESSION'' NUMEXPRESSION
EXPRESSION' -> = EXPRESSION'' NUMEXPRESSION
EXPRESSION' -> > EXPRESSION'' NUMEXPRESSION
EXPRESSION' -> ''

EXPRESSION'' -> =
EXPRESSION'' -> ''

NUMEXPRESSION -> TERM OPTERM

OPTERM -> - TERM OPTERM 
OPTERM -> + TERM OPTERM 
OPTERM -> ''

TERM -> UNARYEXPR UNARYTERM

UNARYTERM -> * UNARYEXPR UNARYTERM 
UNARYTERM -> / UNARYEXPR UNARYTERM
UNARYTERM -> % UNARYEXPR UNARYTERM 
UNARYTERM -> ''

UNARYEXPR -> FACTOR 
UNARYEXPR -> + FACTOR 
UNARYEXPR -> - FACTOR

FACTOR -> int_constant 
FACTOR -> float_constant 
FACTOR -> string_constant
FACTOR -> null 
FACTOR -> LVALUE 
FACTOR -> ( NUMEXPRESSION )

LVALUE -> ident NUMEXPRLIST
